## teb

![1](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/1.png)

GetLastError里面



[00000018h]拿到peb



thread enviroment block，线程环境块

是个结构体



### dt，windbg里查看结构体信息的

![2](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/2.png)

![3](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/3.png)

![4](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/4.png)

每个线程有一个teb，每个进程也有一个peb

## peb

![5](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/5.png)

第二次打开这个文件，前面的断了

![6](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/6.png)

![7](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/7.png)

BeingDebugged是1，当前是被调试状态

ImageBaseAddress，模块基址

![8](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/8.png)

ProcessHeaps，堆

NtGlobalFlag，经常用来做反调试。设置了这个标志后，这个程序进程很多地方的内存就跟没有调试状态的进程的内存不一样了

比如堆，堆初始化的值和release之后的值(FD或者其他)都跟NtGlobalFlag有关

### 重点是LDR

![9](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/9.png)

GetModuleHandle,拿模块句柄

GetModuleFileName,拿模块的文件路径

这些东西都存到LDR里面的

![10](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/10.png)

加载顺序

内存顺序

初始化顺序

![11](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/11.png)

这个结构体是两个双向链表的指针

#### 找InLoadOrderModuleList的地址

先找ldr的地址

![12](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/12.png)

![14](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/14.png)

0C到14，14到1C，1C到24，都是8个字节，两个dword

![15](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/15.png)

![16](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/16.png)

00400000，模块基址，00401000，模块的入口地址

![17](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/17.png)

du看UNICODE，这是模块路径

![18](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/18.png)

![19](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/19.png)

字符串长度00e2，缓冲区大小00e0

![20](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/20.png)

![21](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/21.png)

这也是一个UNICODE

![22](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/22.png)

当前模块在内存中的大小

![23](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/23.png)

ldr的第一个dword是ntdll

![24](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/24.png)

继续往前面找，是kernel32

![25](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/25.png)

再往前走，kernelbase

![26](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/26.png)

再往前走，user32

![27](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/27.png)

其实就是lm里面所有的dll，依次加载

![28](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/28.png)

![29](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/29.png)

是一个双向循环链表，加载那些dll

要记住前三个，主模块，ntdll，kernel32，这三个顺序不会变

![30](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/30.png)

这里FS现在可以理解为一块堆地址，18是偏移

### 记录

```txt
+0  上一个dll的地址
+4  下一个dll的地址
+18 模块句柄(hInstance,hModule)
+1C	模块入口点
+20 模块占内存空间的大小
+24 模块全路径(UNICODE)
+2c 文件名(.exe)
```

#### 解析FS机制

GetThreadSelectorEntry

用一个OD(1)调试打开另一个OD(2)，用打开的OD去调试程序，OD1会得到FS，OD1在GetThreadSelectorEntry下断点，再判断GetThreadSelectorEntry拿到的东西，做了哪些解析

![31](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/31.png)

teb，偏移为18的位置存了自己的首地址。也就是FS:[18]

## windbg快捷拿teb，peb地址

```bash
!teb
!peb
```

![13](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/13.png)

## seh

structure exception handler，结构化异常处理



筛选器异常是最后处理异常的机会



seh是针对某个函数去处理异常。某函数发生异常之后，系统会调一个回调函数



excpt.h这个头文件中，有这个回调函数的声明和返回值

```cpp
    EXCEPTION_DISPOSITION __cdecl _except_handler(
        _In_ struct _EXCEPTION_RECORD* _ExceptionRecord,	// 异常记录
        _In_ void*                     _EstablisherFrame,	// 忽略，嵌套异常使用的
        _Inout_ struct _CONTEXT*       _ContextRecord,	 	
        _Inout_ void*                  _DispatcherContext	// 忽略，异常展开用的
        );

// 异常回调的返回值
typedef enum _EXCEPTION_DISPOSITION
{
    ExceptionContinueExecution,		// 异常处理过了，继续执行
    ExceptionContinueSearch,		// 不处理异常，其他地方处理
    ExceptionNestedException,
    ExceptionCollidedUnwind
} EXCEPTION_DISPOSITION;
```

有了seh之后，异常的派发流程就变了

->调试器->SEH->调试器->筛选器异常->系统(杀死进程)

**C++的try...catch...**是在当前这些异常机制上面又封装了一层

![32](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/9/32.png)

生成PDB，调试的时候可以看到函数名

```asm
.586
.model flat,stdcall
option casemap:none

   include windows.inc
   include user32.inc
   include kernel32.inc
   
   includelib user32.lib
   includelib kernel32.lib

;异常节点
Node struct
    next dd 0
    handler dd 0
    
Node ends

.data
    g_sz0 db "0 异常来了", 0
    g_sz1 db "1 异常来了", 0


.code

Handler1 proc pER:ptr EXCEPTION_RECORD, pEstablisherFrame:DWORD, pCtx:ptr CONTEXT, pDispatcherContext:DWORD
    
    invoke MessageBox, NULL, offset g_sz1, NULL, MB_OK
    
    mov ebx, pER
    assume ebx:ptr EXCEPTION_RECORD
    
    mov esi, pCtx
    assume esi:ptr CONTEXT
    
    .if [ebx].ExceptionCode == EXCEPTION_INT_DIVIDE_BY_ZERO    ;除0异常
    
        add [esi].regEip, 2     ;跳过异常代码
    .endif
    
    assume ebx:nothing
    assume esi:nothing
    
    mov eax, ExceptionContinueExecution
    
    ret

Handler1 endp

Func1 proc
    LOCAL @node:Node
    
    mov @node.handler, offset Handler1
    
    assume fs:nothing
    lea eax, @node
    mov fs:[0], eax
    
    xor esi, esi
    div esi
    
    ret

Func1 endp

Handler0 proc pER:ptr EXCEPTION_RECORD, pEstablisherFrame:DWORD, pCtx:ptr CONTEXT, pDispatcherContext:DWORD
    
    invoke MessageBox, NULL, offset g_sz0, NULL, MB_OK
    
    mov ebx, pER
    assume ebx:ptr EXCEPTION_RECORD
    
    mov esi, pCtx
    assume esi:ptr CONTEXT
    
    .if [ebx].ExceptionCode == EXCEPTION_ACCESS_VIOLATION   ;访问空地址异常
    
        add [esi].regEip, 2     ;跳过异常代码
    .endif
    
    assume ebx:nothing
    assume esi:nothing
    
    mov eax, ExceptionContinueExecution     ;已经处理过异常了。继续运行
    ;mov eax, ExceptionContinueSearch       ;交给调试器或者筛选器处理
    
    ret

Handler0 endp

Func0 proc
    LOCAL @node:Node
    
    ;安装异常
    mov @node.handler, offset Handler0
    
    assume fs:nothing   ;系统默认对fs有解释，让系统不解释
    lea eax, @node
    mov fs:[0], eax     ;发生异常的时候，系统会从fs:[0]拿异常节点的值，再从值中拿异常回调函数
    
    invoke Func1
    
    xor eax, eax
    mov [eax], eax      ;异常的代码
        
    ret

Func0 endp

start:
    invoke Func0
    
    invoke MessageBox, NULL, NULL, NULL, MB_OK
	
	xor eax, eax
	invoke ExitProcess,eax


end start

```

调Func0的时候，写了一次FS:[0]，调Func1的时候，又写了一次FS:[0]。Func1处理完异常回到Func0的时候，FS:[0]还是Func1的异常回调。

所以应该进函数写一次FS:[0]，出函数恢复一次FS:[0]

```asm
.586
.model flat,stdcall
option casemap:none

   include windows.inc
   include user32.inc
   include kernel32.inc
   
   includelib user32.lib
   includelib kernel32.lib

;异常节点
Node struct
    next dd 0
    handler dd 0
    
Node ends

.data
    g_sz0 db "0 异常来了", 0
    g_sz1 db "1 异常来了", 0


.code

Handler1 proc pER:ptr EXCEPTION_RECORD, pEstablisherFrame:DWORD, pCtx:ptr CONTEXT, pDispatcherContext:DWORD
    
    invoke MessageBox, NULL, offset g_sz1, NULL, MB_OK
    
    mov ebx, pER
    assume ebx:ptr EXCEPTION_RECORD
    
    mov esi, pCtx
    assume esi:ptr CONTEXT
    
    .if [ebx].ExceptionCode == EXCEPTION_INT_DIVIDE_BY_ZERO    ;除0异常
    
        add [esi].regEip, 2     ;跳过异常代码
    .endif
    
    assume ebx:nothing
    assume esi:nothing
    
    mov eax, ExceptionContinueExecution
    
    ret

Handler1 endp

Func1 proc
    LOCAL @node:Node
    LOCAL @pre:dword
    
    ;安装seh
    mov @node.handler, offset Handler1
    
    assume fs:nothing
    mov eax, fs:[0]
    mov @pre, eax
    
    lea eax, @node
    mov fs:[0], eax
    
    xor esi, esi
    div esi
    
    ;卸载seh
    mov eax, @pre
    mov fs:[0], eax
    
    ret

Func1 endp

Handler0 proc pER:ptr EXCEPTION_RECORD, pEstablisherFrame:DWORD, pCtx:ptr CONTEXT, pDispatcherContext:DWORD
    
    invoke MessageBox, NULL, offset g_sz0, NULL, MB_OK
    
    mov ebx, pER
    assume ebx:ptr EXCEPTION_RECORD
    
    mov esi, pCtx
    assume esi:ptr CONTEXT
    
    .if [ebx].ExceptionCode == EXCEPTION_ACCESS_VIOLATION   ;访问空地址异常
    
        add [esi].regEip, 2     ;跳过异常代码
    .endif
    
    assume ebx:nothing
    assume esi:nothing
    
    mov eax, ExceptionContinueExecution     ;已经处理过异常了。继续运行
    ;mov eax, ExceptionContinueSearch       ;交给调试器或者筛选器处理
    
    ret

Handler0 endp

Func0 proc
    LOCAL @node:Node
    LOCAL @pre:dword
    
    ;安装seh
    mov @node.handler, offset Handler0
    
    assume fs:nothing   ;系统默认对fs有解释，让系统不解释
    mov eax, fs:[0]
    mov @pre, eax
    
    lea eax, @node
    mov fs:[0], eax     ;发生异常的时候，系统会从fs:[0]拿异常节点的值，再从值中拿异常回调函数
    
    invoke Func1
    
    xor eax, eax
    mov [eax], eax      ;异常的代码
    
    ;卸载seh
    mov eax, @pre
    mov fs:[0], eax
        
    ret

Func0 endp

start:
    invoke Func0
    
    invoke MessageBox, NULL, NULL, NULL, MB_OK
	
	xor eax, eax
	invoke ExitProcess,eax


end start

```

### node里面next的用处

```asm
.586
.model flat,stdcall
option casemap:none

   include windows.inc
   include user32.inc
   include kernel32.inc
   
   includelib user32.lib
   includelib kernel32.lib

;异常节点
Node struct
    next dd 0
    handler dd 0
    
Node ends

.data
    g_sz0 db "0 异常来了", 0
    g_sz1 db "1 异常来了", 0


.code

Handler1 proc pER:ptr EXCEPTION_RECORD, pEstablisherFrame:DWORD, pCtx:ptr CONTEXT, pDispatcherContext:DWORD
    
    invoke MessageBox, NULL, offset g_sz1, NULL, MB_OK
    
    mov ebx, pER
    assume ebx:ptr EXCEPTION_RECORD
    
    mov esi, pCtx
    assume esi:ptr CONTEXT
    
    .if [ebx].ExceptionCode == EXCEPTION_INT_DIVIDE_BY_ZERO     ;除0异常
    
        add [esi].regEip, 2                                     ;跳过异常代码
    .elseif [ebx].ExceptionCode == EXCEPTION_ACCESS_VIOLATION
        mov eax, ExceptionContinueSearch
        ret                                                     ;不处理这个内存访问异常
    .endif
    
    assume ebx:nothing
    assume esi:nothing
    
    mov eax, ExceptionContinueExecution
    
    ret

Handler1 endp

Func1 proc
    LOCAL @node:Node
    LOCAL @pre:dword
    
    ;安装seh
    mov @node.handler, offset Handler1
    
    assume fs:nothing
    mov eax, fs:[0]
    mov @pre, eax
    
    lea eax, @node
    mov fs:[0], eax
    
    xor esi, esi
    div esi
    
    xor eax, eax
    mov [eax], eax      ;再来一个内存访问异常
    
    ;卸载seh
    mov eax, @pre
    mov fs:[0], eax
    
    ret

Func1 endp

Handler0 proc pER:ptr EXCEPTION_RECORD, pEstablisherFrame:DWORD, pCtx:ptr CONTEXT, pDispatcherContext:DWORD
    
    invoke MessageBox, NULL, offset g_sz0, NULL, MB_OK
    
    mov ebx, pER
    assume ebx:ptr EXCEPTION_RECORD
    
    mov esi, pCtx
    assume esi:ptr CONTEXT
    
    .if [ebx].ExceptionCode == EXCEPTION_ACCESS_VIOLATION   ;访问空地址异常
    
        add [esi].regEip, 2     ;跳过异常代码
    .endif
    
    assume ebx:nothing
    assume esi:nothing
    
    mov eax, ExceptionContinueExecution     ;已经处理过异常了。继续运行
    ;mov eax, ExceptionContinueSearch       ;交给调试器或者筛选器处理
    
    ret

Handler0 endp

Func0 proc
    LOCAL @node:Node
    LOCAL @pre:dword
    
    ;安装seh
    mov @node.handler, offset Handler0
    
    assume fs:nothing   ;系统默认对fs有解释，让系统不解释
    mov eax, fs:[0]
    mov @pre, eax
    
    lea eax, @node
    mov fs:[0], eax     ;发生异常的时候，系统会从fs:[0]拿异常节点的值，再从值中拿异常回调函数
    
    invoke Func1
    
    xor eax, eax
    mov [eax], eax      ;异常的代码
    
    ;卸载seh
    mov eax, @pre
    mov fs:[0], eax
        
    ret

Func0 endp

start:
    invoke Func0
    
    invoke MessageBox, NULL, NULL, NULL, MB_OK
	
	xor eax, eax
	invoke ExitProcess,eax


end start

```

这样的话，func0调到func1，func1有两个异常，第二个异常不处理让其他处理，但是这时候fs:[0]只有handler1，没有其他的异常回调

所以

```asm
.586
.model flat,stdcall
option casemap:none

   include windows.inc
   include user32.inc
   include kernel32.inc
   
   includelib user32.lib
   includelib kernel32.lib

;异常节点
Node struct
    next dd 0
    handler dd 0
    
Node ends

.data
    g_sz0 db "0 异常来了", 0
    g_sz1 db "1 异常来了", 0


.code

Handler1 proc pER:ptr EXCEPTION_RECORD, pEstablisherFrame:DWORD, pCtx:ptr CONTEXT, pDispatcherContext:DWORD
    
    invoke MessageBox, NULL, offset g_sz1, NULL, MB_OK
    
    mov ebx, pER
    assume ebx:ptr EXCEPTION_RECORD
    
    mov esi, pCtx
    assume esi:ptr CONTEXT
    
    .if [ebx].ExceptionCode == EXCEPTION_INT_DIVIDE_BY_ZERO     ;除0异常
    
        add [esi].regEip, 2                                     ;跳过异常代码
    .elseif [ebx].ExceptionCode == EXCEPTION_ACCESS_VIOLATION
        mov eax, ExceptionContinueSearch
        ret                                                     ;不处理这个内存访问异常
    .endif
    
    assume ebx:nothing
    assume esi:nothing
    
    mov eax, ExceptionContinueExecution
    
    ret

Handler1 endp

Func1 proc
    LOCAL @node:Node
    
    ;安装seh
    mov @node.handler, offset Handler1
    
    assume fs:nothing
    mov eax, fs:[0]
    mov @node.next, eax
    
    lea eax, @node
    mov fs:[0], eax
    
    xor esi, esi
    div esi
    
    xor eax, eax
    mov [eax], eax      ;再来一个内存访问异常
    
    ;卸载seh
    mov eax, @node.next
    mov fs:[0], eax
    
    ret

Func1 endp

Handler0 proc pER:ptr EXCEPTION_RECORD, pEstablisherFrame:DWORD, pCtx:ptr CONTEXT, pDispatcherContext:DWORD
    
    invoke MessageBox, NULL, offset g_sz0, NULL, MB_OK
    
    mov ebx, pER
    assume ebx:ptr EXCEPTION_RECORD
    
    mov esi, pCtx
    assume esi:ptr CONTEXT
    
    .if [ebx].ExceptionCode == EXCEPTION_ACCESS_VIOLATION   ;访问空地址异常
    
        add [esi].regEip, 2     ;跳过异常代码
    .endif
    
    assume ebx:nothing
    assume esi:nothing
    
    mov eax, ExceptionContinueExecution     ;已经处理过异常了。继续运行
    ;mov eax, ExceptionContinueSearch       ;交给调试器或者筛选器处理
    
    ret

Handler0 endp

Func0 proc
    LOCAL @node:Node
    
    ;安装seh
    mov @node.handler, offset Handler0
    
    assume fs:nothing   ;系统默认对fs有解释，让系统不解释
    mov eax, fs:[0]
    mov @node.next, eax
    
    lea eax, @node
    mov fs:[0], eax     ;发生异常的时候，系统会从fs:[0]拿异常节点的值，再从值中拿异常回调函数
    
    invoke Func1
    
    xor eax, eax
    mov [eax], eax      ;异常的代码
    
    ;卸载seh
    mov eax, @node.next
    mov fs:[0], eax
        
    ret

Func0 endp

start:
    invoke Func0
    
    invoke MessageBox, NULL, NULL, NULL, MB_OK
	
	xor eax, eax
	invoke ExitProcess,eax


end start

```

先func0，到func1，func1先有一个除0异常，进handler1处理掉了，后有一个内存访问异常，自己不处理，沿着seh异常链到handler0处理掉，回到func0，有一个内存访问异常，到handler0处理掉了

最终写法

```asm
.586
.model flat,stdcall
option casemap:none

   include windows.inc
   include user32.inc
   include kernel32.inc
   
   includelib user32.lib
   includelib kernel32.lib

.data
    g_sz0 db "0 异常来了", 0
    g_sz1 db "1 异常来了", 0
    
.code

Handler1 proc pER:ptr EXCEPTION_RECORD, pEstablisherFrame:DWORD, pCtx:ptr CONTEXT, pDispatcherContext:DWORD
    
    invoke MessageBox, NULL, offset g_sz1, NULL, MB_OK
    
    mov ebx, pER
    assume ebx:ptr EXCEPTION_RECORD
    
    mov esi, pCtx
    assume esi:ptr CONTEXT
    
    .if [ebx].ExceptionCode == EXCEPTION_INT_DIVIDE_BY_ZERO     ;除0异常
    
        add [esi].regEip, 2                                     ;跳过异常代码
    .elseif [ebx].ExceptionCode == EXCEPTION_ACCESS_VIOLATION
        mov eax, ExceptionContinueSearch
        ret                                                     ;不处理这个内存访问异常
    .endif
    
    assume ebx:nothing
    assume esi:nothing
    
    mov eax, ExceptionContinueExecution
    
    ret

Handler1 endp

Func1 proc

    assume fs:nothing
    push offset Handler1
    push fs:[0]
    mov fs:[0], esp
    
    xor esi, esi
    div esi
    
    xor eax, eax
    mov [eax], eax      ;再来一个内存访问异常
    
    ;卸载seh
    pop fs:[0]
    add esp, 4
    
    ret

Func1 endp

Handler0 proc pER:ptr EXCEPTION_RECORD, pEstablisherFrame:DWORD, pCtx:ptr CONTEXT, pDispatcherContext:DWORD
    
    invoke MessageBox, NULL, offset g_sz0, NULL, MB_OK
    
    mov ebx, pER
    assume ebx:ptr EXCEPTION_RECORD
    
    mov esi, pCtx
    assume esi:ptr CONTEXT
    
    .if [ebx].ExceptionCode == EXCEPTION_ACCESS_VIOLATION   ;访问空地址异常
    
        add [esi].regEip, 2     ;跳过异常代码
    .endif
    
    assume ebx:nothing
    assume esi:nothing
    
    mov eax, ExceptionContinueExecution     ;已经处理过异常了。继续运行
    ;mov eax, ExceptionContinueSearch       ;交给调试器或者筛选器处理
    
    ret

Handler0 endp

Func0 proc

    assume fs:nothing
    push offset Handler0
    push fs:[0]
    mov fs:[0], esp

    invoke Func1
    
    xor eax, eax
    mov [eax], eax      ;异常的代码
    
    ;卸载seh
    pop fs:[0]
    add esp, 4
        
    ret

Func0 endp

start:
    invoke Func0
    
    invoke MessageBox, NULL, NULL, NULL, MB_OK
	
	xor eax, eax
	invoke ExitProcess,eax


end start

```

## wetype_tip.dll与输入法有关

输入法hook了系统API
