## 钢琴

**运行起来，查看窗口**

![1](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/1.png)

**找过程函数**

![2](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/2.png)

**找到keydown消息**

![3](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/3.png)

+4是返回地址，+8是第一个参数，+c是第二个参数，+10是第三个参数wParam

可以判断消息类型

![54](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/54.png)

![4](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/4.png)

![55](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/55.png)

有个playOneSound，我们模仿他调用



**我们现在有一个piano_dll.dll，但是这个dll没有lib，可以新建一个同名dll项目，相同格式地导出fun_playOneSound函数，生成的lib可以给老dll使用**

```asm
.586
.model flat,stdcall
option casemap:none

   include windows.inc
   include user32.inc
   include kernel32.inc
   
   includelib user32.lib
   includelib kernel32.lib
   
.code
;声明这个函数
fun_playOneSound proc C arg0:dword
    
    
    xor eax, eax
    ret

fun_playOneSound endp

;DllMain
DllMain proc hinstDLL:HINSTANCE, fdwReason:DWORD, lpvReserved:LPVOID
    .if fdwReason == DLL_PROCESS_ATTACH
         
    .endif
    
    ret
DllMain endp


end DllMain
```

### 写一个asm模仿他调用playOneSound

需要将dll复制到这个项目文件夹中

```asm
.586
.model flat,stdcall
option casemap:none

   include windows.inc
   include user32.inc
   include kernel32.inc
   
   includelib user32.lib
   includelib kernel32.lib
   
   includelib piano_dll.lib


WinMain proto :DWORD,:DWORD,:DWORD,:DWORD

;声明lib里的这个函数
fun_playOneSound proto C :dword


.data
   ClassName db "MainWinClass",0
   AppName  db "Main Window",0

.data?
   hInstance HINSTANCE ?
   CommandLine LPSTR ?

.code


; ---------------------------------------------------------------------------


start:
	invoke GetModuleHandle, NULL	;拿当前进程句柄
	mov    hInstance,eax
	
	invoke GetCommandLine	;获取启动当前进程的完整命令行
	mov    CommandLine,eax
	
	;WinMain
	invoke WinMain, hInstance,NULL,CommandLine, SW_SHOWDEFAULT
	invoke ExitProcess,eax	;退出进程

WinMain proc hInst:HINSTANCE,hPrevInst:HINSTANCE,CmdLine:LPSTR,CmdShow:DWORD
	LOCAL wc:WNDCLASSEX
	LOCAL msg:MSG
	LOCAL hwnd:HWND
	
	;设计窗口类
	mov   wc.cbSize,SIZEOF WNDCLASSEX
	mov   wc.style, CS_HREDRAW or CS_VREDRAW
	mov   wc.lpfnWndProc, OFFSET WndProc
	mov   wc.cbClsExtra,NULL
	mov   wc.cbWndExtra,NULL
	push  hInstance
	pop   wc.hInstance
	mov   wc.hbrBackground,COLOR_BTNFACE+1
	mov   wc.lpszMenuName,NULL
	mov   wc.lpszClassName,OFFSET ClassName
	
	;加载图标
	invoke LoadIcon,NULL,IDI_APPLICATION
	mov   wc.hIcon,eax
	mov   wc.hIconSm,eax
	
	;加载光标
	invoke LoadCursor,NULL,IDC_ARROW
	mov   wc.hCursor,eax
	
	;注册窗口类
	invoke RegisterClassEx, addr wc
	;创建窗口
	INVOKE CreateWindowEx,NULL,ADDR ClassName,ADDR AppName,\
           WS_OVERLAPPEDWINDOW,CW_USEDEFAULT,\
           CW_USEDEFAULT,CW_USEDEFAULT,CW_USEDEFAULT,NULL,NULL,\
           hInst,NULL
	mov   hwnd,eax
	
	;显示、更新窗口
	invoke ShowWindow, hwnd,SW_SHOWNORMAL
	invoke UpdateWindow, hwnd
	
	;消息循环
	.WHILE TRUE
		invoke GetMessage, ADDR msg,NULL,0,0
		.BREAK .IF (!eax)
		invoke TranslateMessage, ADDR msg
		invoke DispatchMessage, ADDR msg
	.ENDW
	
	mov     eax,msg.wParam
	ret
WinMain endp

WndProc proc hWnd:HWND, uMsg:UINT, wParam:WPARAM, lParam:LPARAM
	
	.IF uMsg==WM_DESTROY
		invoke PostQuitMessage,NULL
	.ELSEIF uMsg==WM_CREATE
	
	;按键是字母，就调fun_playOneSound
    .elseif uMsg == WM_KEYDOWN
        .if wParam >= 'A' && wParam <= 'Z'
            invoke fun_playOneSound, wParam
        .endif
		;
	.ELSE
		invoke DefWindowProc,hWnd,uMsg,wParam,lParam		
		ret
	.ENDIF
	
	xor eax,eax
	ret
WndProc endp


end start
```

但是没有调成功

Piano.exe里查看,可执行模块,piano_dll.dll,显示名称,发现Create函数，可能是初始化的



![5](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/5.png)

查看,可执行模块,Piano.exe主模块，查看名称,

![6](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/6.png)

右键查找输入函数参考,只有一个位置调了

![7](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/7.png)

![8](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/8.png)

发现WM_CREATE消息里面调的



![9](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/9.png)

发现参数是esi,从上面跳过来,esi是ebp+8的内容,就是第一个参数,就是窗口句柄

![10](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/10.png)

![11](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/11.png)

没有看到create函数平栈



后面又调了create函数后面的09711A0这个函数，两个函数一起平栈了,add esp, 8



重新将dll中的lib导出,在asm里调用一下,发现出现声音

```asm
.586
.model flat,stdcall
option casemap:none

   include windows.inc
   include user32.inc
   include kernel32.inc
   
   includelib user32.lib
   includelib kernel32.lib
   
.code
fun_playOneSound proc C arg0:dword
    
    
    xor eax, eax
    ret

fun_playOneSound endp

;导出fun_Create
fun_Create proc C arg0:DWORD
    
    xor eax,eax
    ret

fun_Create endp

DllMain proc hinstDLL:HINSTANCE, fdwReason:DWORD, lpvReserved:LPVOID
    .if fdwReason == DLL_PROCESS_ATTACH
         
    .endif
    
    ret
DllMain endp


end DllMain
```

```asm
.586
.model flat,stdcall
option casemap:none

   include windows.inc
   include user32.inc
   include kernel32.inc
   
   includelib user32.lib
   includelib kernel32.lib
   
   includelib piano_dll.lib


WinMain proto :DWORD,:DWORD,:DWORD,:DWORD

fun_playOneSound proto C :dword
fun_Create proto C arg0:dword

.data
   ClassName db "MainWinClass",0
   AppName  db "Main Window",0

.data?
   hInstance HINSTANCE ?
   CommandLine LPSTR ?

.code


; ---------------------------------------------------------------------------


start:
	invoke GetModuleHandle, NULL
	mov    hInstance,eax
	
	invoke GetCommandLine
	mov    CommandLine,eax
	
	invoke WinMain, hInstance,NULL,CommandLine, SW_SHOWDEFAULT
	invoke ExitProcess,eax

WinMain proc hInst:HINSTANCE,hPrevInst:HINSTANCE,CmdLine:LPSTR,CmdShow:DWORD
	LOCAL wc:WNDCLASSEX
	LOCAL msg:MSG
	LOCAL hwnd:HWND
	
	mov   wc.cbSize,SIZEOF WNDCLASSEX
	mov   wc.style, CS_HREDRAW or CS_VREDRAW
	mov   wc.lpfnWndProc, OFFSET WndProc
	mov   wc.cbClsExtra,NULL
	mov   wc.cbWndExtra,NULL
	push  hInstance
	pop   wc.hInstance
	mov   wc.hbrBackground,COLOR_BTNFACE+1
	mov   wc.lpszMenuName,NULL
	mov   wc.lpszClassName,OFFSET ClassName
	
	invoke LoadIcon,NULL,IDI_APPLICATION
	mov   wc.hIcon,eax
	mov   wc.hIconSm,eax
	
	invoke LoadCursor,NULL,IDC_ARROW
	mov   wc.hCursor,eax
	
	invoke RegisterClassEx, addr wc
	INVOKE CreateWindowEx,NULL,ADDR ClassName,ADDR AppName,\
           WS_OVERLAPPEDWINDOW,CW_USEDEFAULT,\
           CW_USEDEFAULT,CW_USEDEFAULT,CW_USEDEFAULT,NULL,NULL,\
           hInst,NULL
	mov   hwnd,eax
	
	invoke ShowWindow, hwnd,SW_SHOWNORMAL
	invoke UpdateWindow, hwnd
	
	.WHILE TRUE
		invoke GetMessage, ADDR msg,NULL,0,0
		.BREAK .IF (!eax)
		invoke TranslateMessage, ADDR msg
		invoke DispatchMessage, ADDR msg
	.ENDW
	
	mov     eax,msg.wParam
	ret
WinMain endp

WndProc proc hWnd:HWND, uMsg:UINT, wParam:WPARAM, lParam:LPARAM
	
	.IF uMsg==WM_DESTROY
		invoke PostQuitMessage,NULL
	.ELSEIF uMsg==WM_CREATE		;WM_CREATE消息里调用fun_Create
	    invoke fun_Create, hWnd

    .elseif uMsg == WM_KEYDOWN
        .if wParam >= 'A' && wParam <= 'Z'
            invoke fun_playOneSound, wParam
        .endif
		;
	.ELSE
		invoke DefWindowProc,hWnd,uMsg,wParam,lParam		
		ret
	.ENDIF
	
	xor eax,eax
	ret
WndProc endp


end start

```

猜他会读文件

右键，找unicode字符串

![12](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/12.png)

进行了文件操作



![13](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/13.png)

文件操作开头的地方，右键,转到,更多跳转调用

有很多地方调这个文件操作

![14](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/14.png)

![15](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/15.png)

添加标签

![16](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/16.png)

发现这么多CALL，每个地方都是调用的这个文件操作函数

不过不同的call,参数不同，push1,2,3,4

![17](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/17.png)

很像switch case

![18](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/18.png)

看一下这个调文件操作case。往上翻

![19](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/19.png)

数组的下标的寻址公式

![20](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/20.png)

右键,数据窗口中跟随,右键,长型,地址



![21](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/21.png)

比较eax是不是8004，是就跳到数组寻址的位置

eax来自于哪里呢，往上翻

![22](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/22.png)

eax和8003比较,才跳到刚才那里

ebp+10是wparam

wparam的低位是COMMAND消息	movzx eax,cx意思是只保留eax的低两字节

COMMAND消息来源有菜单，快捷键，这里菜单里没有任何东西

![23](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/23.png)

32771就是16进制的8003.32772就是8004



![24](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/24.png)

所以这里就是比较快捷键，比较完之后就进行文件操作

![25](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/25.png)

打开文件,EAX有值

![26](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/26.png)

FSEEK到SEEK_END

C语言特有的拿文件大小的方式

![27](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/27.png)

new缓冲区

![28](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/28.png)

new的缓冲区的内容存到全局变量了

也就是这个全局变量指向new的缓冲区

![29](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/29.png)

读文件到刚才new的缓冲区，后面就返回了



对全局变量查找参考

![30](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/30.png)

![31](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/31.png)

![32](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/32.png)

只有唯一一个有用的调用全局变量的地方，其他的都是删除缓冲区的

![33](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/33.png)

发现来自于KEYDOWN消息

![34](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/34.png)

感觉这里可能是关键点

![35](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/35.png)

只有一个地方是1

![36](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/36.png)

case 8003的时候

8003是Ctrl+J

![38](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/38.png)

画了个点

再随便按个按钮

![39](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/39.png)

发现从缓冲区读了乐谱出来(就是之前打开文件读取到缓冲区的)

![40](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/40.png)

数组遍历，edx是数组首地址，ecx是偏移，从0开始，41是A，5A是Z

下面又比较了一些，然后就playOneSound

![41](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/41.png)

除了一个东西

![42](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/42.png)

找到这个地址被赋值的地方

![43](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/43.png)

原来除的是文件大小。文件大小放到了[974510]

逻辑就是：按ctrl+1,2,3,4...就是读取歌单，按ctrl+j就是开启作弊，按任意字符就是歌单里的字符

## 筛选器异常

也叫最终异常。在try...catch...的后面



写内存失败、打开文件出错。。。就叫筛选器异常，可以用api，调我们自己的函数来处理这个异常

![44](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/44.png)

参数看起来像结构体，实际是个函数指针，函数的参数是个结构体指针



函数的原型：

![45](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/45.png)

![46](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/46.png)

结构体有两个成员，两个成员也是结构体

```
PEXCEPTION_RECORD 异常记录信息，里面记录了异常相关的信息
PCONTEXT 异常发送时的各个寄存器的信息
```

![47](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/47.png)

异常记录信息结构体的成员

异常码，0xC00000005L

异常的标志

第二个参数和第三个参数用不上，异常里套异常的时候才有用

发生异常的地址，产生异常那行代码的EIP

最后两个参数是一个长度，一个数组，相当于变长数组。一般的异常都用不上他，0xC00000005L内存访问异常才看他

![48](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/48.png)

发生0xC00000005L的时候，这个数组的前两个DWORD是有用的，第一个DWORD指明了异常产生的原因，第二个DWORD指明了内存访问异常产生的地址

![56](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/56.png)



![49](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/49.png)

OD和X64DBG都不能调用异常的回调代码

加一个sharpOD插件

![50](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/50.png)

![51](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/51.png)

```asm
MyUnhandledExceptionFilter proc pEP:ptr EXCEPTION_POINTERS
    LOCAL @pEr:ptr EXCEPTION_RECORD
    LOCAL @pCtx:ptr CONTEXT 
    
    mov ebx, pEP	;参数(结构体指针)
    assume ebx:ptr EXCEPTION_POINTERS	;类型
    
    mov eax, [ebx].pExceptionRecord		;结构体的第一个成员,还是一个结构体
    mov @pEr, eax
    
    mov eax, [ebx].ContextRecord		;结构体的第二个成员,还是一个结构体
    mov @pCtx, eax
    
    ;调MessageBox
    invoke MessageBox, NULL, offset g_szMsg, NULL, MB_OKCANCEL
MyUnhandledExceptionFilter endp
```

这是异常的函数内部。选中这一行的上一行得到的EAX是异常记录

![52](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/52.png)

异常码

异常的代码地址



最后两个参数的第一个参数是1，表示写内存出错，想写的地址是0



### 异常的返回值

![53](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/7/53.png)

第一个是告诉系统，这个异常**处理了**，这个进程可以结束了

第二个是这个异常我**不处理**，交给别人处理(调试状态，**交给调试器**，非调试状态，**直接结束进程**)

第三个是告诉系统，这个异常**已经处理**了，程序可以正常**继续运行**



```asm
.586
.model flat,stdcall
option casemap:none

   include windows.inc
   include user32.inc
   include kernel32.inc
   
   includelib user32.lib
   includelib kernel32.lib


WinMain proto :DWORD,:DWORD,:DWORD,:DWORD


.data
    g_szMsg db "异常来了, 是否跳过异常代码", 0

.code
MyUnhandledExceptionFilter proc pEP:ptr EXCEPTION_POINTERS
    LOCAL @pEr:ptr EXCEPTION_RECORD
    LOCAL @pCtx:ptr CONTEXT 
    
    mov ebx, pEP	;参数(结构体指针)
    assume ebx:ptr EXCEPTION_POINTERS	;类型
    
    mov eax, [ebx].pExceptionRecord		;结构体的第一个成员,还是一个结构体
    mov @pEr, eax
    
    mov eax, [ebx].ContextRecord		;结构体的第二个成员,还是一个结构体
    mov @pCtx, eax
    
    ;调MessageBox
    invoke MessageBox, NULL, offset g_szMsg, NULL, MB_OKCANCEL
    .if eax == IDOK
        mov ebx, @pCtx		;结构体的第二个成员
        assume ebx:ptr CONTEXT 
        
        add [ebx].regEip, 2		;异常发生的那行代码的地址+2。跳过。因为xor eax, eax汇编是两字节
        assume ebx:nothing
    .endif
    
    ;mov eax, EXCEPTION_EXECUTE_HANDLER
    mov eax, EXCEPTION_CONTINUE_EXECUTION	;点取消，就继续运行
    
    ret
MyUnhandledExceptionFilter endp

start:
	;筛选器异常API，参数是个函数指针
    invoke SetUnhandledExceptionFilter, offset MyUnhandledExceptionFilter
    
    xor eax, eax
    mov [eax], eax	;写入0地址的内存

    xor eax, eax
	invoke ExitProcess,eax

end start
```

点是，跳过异常代码，点否，不跳过异常代码

### dmp文件

ProcessHacker可以将发生异常时候的程序，保存为dmp文件

dmp文件里有发生异常的时候的内存，寄存器...信息

分析dmp文件，用windbg
