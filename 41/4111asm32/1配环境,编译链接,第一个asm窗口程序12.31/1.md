32位汇编,就是汇编版的SDK编程

## 环境配置

masm32.com

将C:\masm32\bin和C:\masm32\include和C:\masm32\lib加入环境变量

## 编译和链接

ml /c /coff test.asm

link /subsystem:windows test.obj

### 编译脚本

```bat
echo off

set path=C:\masm32\bin
set include=C:\masm32\include
set lib=C:\masm32\lib

echo off

ml /c /coff test.asm

link /subsystem:windows test.obj
```

### 32位源文件格式

开头这三句都一样

32位有4g内存,不用分段,FLAT平坦

```asm
.386						;这个文件使用的指令集
.model flat, stdcall		;使用的内存模型
option casemap:NONE			;如果是ALL，会将所有的小写转为大写。NONE大小写敏感
```

## 32位不分段，用内存属性来划分，节，内存区，内存块

```txt
.DATA		rw		已初始化全局变量
.CONST		r		只读数据区
.DATA?		rw		未初始化全局变量
.CODE		re		代码
```

## 调试

32位调试用OD或者x64dbg

### od的使用

```txt
u addr 						右键,转到,跟随表达式,填地址		ctrl+g
call,jmp,后面是地址的话 	    直接选中,按回车,就跳过去了,-回返,+重新回去
ctrl+g可以输寄存器的值

a addr						选中语句,右键,汇编,改汇编语句.右键,撤销选择处修改.一直回车可以修改多行
ctrl+a						让选中的不高亮的地方高亮一下

r							双击寄存器窗口的寄存器,直接修改

d							左下角内存窗口ctrl+g,填写地址,转到地址

栈窗口,如果不管他,就是显示的栈顶的位置,ESP值的位置

双击EIP会转到当前IP所在的位置

栈窗口，如果栈这个位置是个代码，可以右键，反汇编窗口中跟随，就会将这个代码反汇编到反汇编窗口
	   如果栈这个位置是个内存，可以右键，数据窗口中跟随，就会将这个内存显示到内存窗口
	   看栈看到很远的位置了，可以右键，转到ESP,回到栈顶
	   
e							改内存，内存窗口选中要改的内存，直接修改。右键，撤销选择处修改

t							f7,单步进入
p 							f8,单步跳过
g							f9,运行

ctrl+f2,重新加载被调试程序

f2下断点
alt+b,查看下过的所有短点.或者查看,查看所有短点.按回车,跳到duan
```

## 32位寄存器

寄存器变成4字节

```TXT
EAX,	AX,	AH,	AL
ECX,	CX,	CH,	CL
EDX,	DX,	DH,	DL
EBX,	BX,	BH,	BL


EBP,	BP
ESP,	SP
ESI,	SI
EDI,	DI
EFLAG,  FLAG
EIP,	IP

高16位只能移位到底16位使用

CS
DS
SS
ES
FS
GS
32位编程基本上用不到段寄存器
```

### 指令

```txt
cwde			ax->eax,扩充
cwq				eax->edx.eax,扩充

lodsd			四字节

stosd			四字节

movsd			四字节

移位指令可以直接跟立即数了

push ebp
mov ebp,esp
sub esp,10				可以换成enter 10,0

push ebp
mov ebp,esp
push esp
sub esp,10				可以换成enter 10,1

mov esp,ebp
pop ebp					可以换成leave

pushad					所用通用寄存器入栈
popad					所用通用寄存器出栈
```

### 寻址

16位的7种寻址都能用,且寄存器都能用

多个一个比例因子寻址,寄存器可以乘以1,2,4,8,可以用作数组寻址

## masm第一个窗口程序

```asm
.386
.model flat, stdcall
option casemap:NONE

include windows.inc
include user32.inc
include gdi32.inc
include kernel32.inc

includelib user32.lib
includelib gdi32.lib
includelib kernel32.lib

.data
    g_szClassName db "AsmWindowClass", 0
    g_szTitle     db "32位汇编的第一个窗口", 0
    g_szTip       db "窗口创建失败", 0

.code
MainWndProc proc hWnd:HWND, nMsg:UINT, wParam:WPARAM, lParam:LPARAM
    .if nMsg == WM_DESTROY
        invoke PostQuitMessage, 0
    .endif

    invoke DefWindowProc, hWnd, nMsg, wParam, lParam

    ret
MainWndProc endp

WinMain proc stdcall hInstance:HINSTANCE
    local @wc:WNDCLASS
    local @hWnd:HWND
    local @msg:MSG

    ;设计窗口类
    mov @wc.style, CS_HREDRAW or CS_VREDRAW		;风格
    mov @wc.lpfnWndProc, offset MainWndProc		;过程函数
    mov @wc.cbClsExtra, 0
    mov @wc.cbWndExtra, 0

    mov eax, hInstance
    mov @wc.hInstance, eax						;实例句柄

    invoke LoadIcon, NULL, IDI_APPLICATION		;加载图标
    mov @wc.hIcon, eax
	
    invoke LoadCursor, NULL, IDC_ARROW			;加载光标
    mov @wc.hCursor, eax

    invoke GetStockObject, WHITE_BRUSH			;画刷
    mov @wc.hbrBackground, eax

    mov @wc.lpszMenuName, NULL					;菜单名

    mov @wc.lpszClassName, offset g_szClassName	;窗口类名

    invoke RegisterClass, addr @wc				;注册窗口类

    ;创建窗口
    invoke CreateWindowEx, NULL, offset g_szClassName, offset g_szTitle, WS_OVERLAPPEDWINDOW, \
        CW_USEDEFAULT, CW_USEDEFAULT, CW_USEDEFAULT, CW_USEDEFAULT, \
        NULL, NULL, hInstance, NULL
    mov @hWnd, eax								;返回窗口句柄
    .if eax == NULL								;创建窗口失败
        invoke MessageBox, NULL, offset g_szTip, offset g_szTitle, MB_OK
        ret
    .endif

    ;显示窗口
    invoke ShowWindow, @hWnd, SW_SHOW

    ;更新窗口
    invoke UpdateWindow, @hWnd

    ;消息循环
    .while TRUE
        invoke GetMessage, addr @msg, NULL, 0, 0
        .if eax == 0
            .break
        .endif

        invoke TranslateMessage, addr @msg
        invoke DispatchMessage, addr @msg
    .endw

    ;过程函数


    ret
WinMain endp


ENTRY:
    invoke GetModuleHandle, NULL
    invoke WinMain, eax
    invoke ExitProcess, 0

end ENTRY
```

### 打开od弹窗历史断点

先x掉,alt+f2,关闭程序,再

删除od目录下的udd文件夹里的内容

插件,cleanupEx,删除*.udd, *.bak就可以了

最后shift+f2重新加载程序

## radasm新建工程，可以复制路径

用VS打开.exe修改

![1](https://alist.hmbb313.top/d/Baidunetdisk/Images/Cracker/41/4111Asm32/1/1.png)
